; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "FluoRender"   
#define MyAppSetupName 'FluoRender'
#define MyAppVersion "2.33"
#define MyAppPublisher "FluoRender"
#define MyAppURL "https://sciinstitute.github.io/fluorender/"
#define MyAppExeName "FluoRender.exe"
#define MyAppID "{CFC06103-F420-4239-9C5D-BA1F71733278}"
#define ISSI_Splash "Images\fluorender_logo_lg.bmp"  
#include "common_include.iss" 

[Setup]
OutputDir=..\build
OutputBaseFilename=FluoRender2.33_win64
SetupIconFile=.\Images\FluoRenderIcon.ico
WizardImageFile=.\Images\fluorender_logo_cut.bmp
WizardSmallImageFile=.\Images\fluorender_logo_square.bmp
WizardImageBackColor=clWhite
LicenseFile=..\..\LICENSE.txt
ChangesAssociations=yes

[Dirs]
Name: "{app}"; 
Name: "{app}\Database"; Permissions: users-full

[Files]
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
; BINARY ITEMS
Source: "..\..\build\bin\Release\FluoRender.exe"; DestDir: "{app}"; Flags: ignoreversion      
; OPEN CL items
Source: "..\..\build\bin\Release\CL_code\*"; DestDir: "{app}\CL_code"; Flags: ignoreversion
; Command items
Source: "..\..\build\bin\Release\Commands\*"; DestDir: "{app}\Commands"; Flags: ignoreversion     
; Data
Source: "..\..\build\bin\Release\Data\*"; DestDir: "{app}\Data"; Flags: ignoreversion
; Database
Source: "..\..\build\bin\Release\Database\*"; DestDir: "{app}\Database"; Flags: ignoreversion; Permissions: users-full
; FONT ITEMS       
Source: "..\..\build\bin\Release\Fonts\*"; DestDir: "{app}\Fonts"; Flags: ignoreversion  
; Java
Source: "..\..\build\bin\Release\Java_Code\*"; DestDir: "{app}\Java_Code"; Flags: ignoreversion  
; Scripts directory
Source: "..\..\build\bin\Release\Scripts\*"; DestDir: "{app}\Scripts"; Flags: ignoreversion  
; Template directory
Source: "..\..\build\bin\Release\Templates\*"; DestDir: "{app}\Templates"; Flags: ignoreversion  
; SETTINGS ITEMS
Source: "..\..\Settings\fluorender.xml"; DestDir: "{app}"; Flags: onlyifdoesntexist; Permissions: users-full
; Benchmark
Source: "..\..\build\bin\Release\FluoRender_benchmark.bat"; DestDir: "{app}"; Flags: ignoreversion      
Source: "..\..\build\bin\Release\FluoRender_benchmark.command"; DestDir: "{app}"; Flags: ignoreversion      
Source: "..\..\build\bin\Release\FluoRender_benchmark_fullscreen.bat"; DestDir: "{app}"; Flags: ignoreversion      
Source: "..\..\build\bin\Release\FluoRender_benchmark_fullscreen.command"; DestDir: "{app}"; Flags: ignoreversion      
; OpenVR      
Source: "..\..\build\bin\Release\openvr_api.dll"; DestDir: "{app}"; Flags: ignoreversion
; Hololens
Source: "..\..\build\bin\Release\Microsoft.Holographic.AppRemoting.OpenXr.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\build\bin\Release\Microsoft.Holographic.AppRemoting.OpenXr.SU.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\build\bin\Release\PerceptionDevice.dll"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\build\bin\Release\OpenXRSpeechGrammar.xml"; DestDir: "{app}"; Flags: ignoreversion
Source: "..\..\build\bin\Release\RemotingXR.json"; DestDir: "{app}"; Flags: ignoreversion

[Registry]
Root: HKCR; Subkey: ".vrp"; ValueType: string; ValueName: ""; ValueData: "FluoRenderProject"; Flags: uninsdeletevalue
Root: HKCR; Subkey: "FluoRenderProject"; ValueType: string; ValueName: ""; ValueData: "FluoRender Project"; Flags: uninsdeletekey
Root: HKCR; Subkey: "FluoRenderProject\DefaultIcon"; ValueType: string; ValueName: ""; ValueData: "{app}\FluoRender.EXE,0"
Root: HKCR; Subkey: "FluoRenderProject\shell\open\command"; ValueType: string; ValueName: ""; ValueData: """{app}\FluoRender.EXE"" ""%1"""

[Code]
function InitializeSetup(): boolean;
var
Version: String;
begin
    Result := BeginSetup('{#MyAppExeName}','{#MyAppID}','{#MyAppVersion}');
    //Result := True;
end;

procedure CurStepChanged(CurStep: TSetupStep);
begin
  if CurStep = ssInstall then
  begin
    DeleteFilesInFolder(ExpandConstant('{app}\CL_code'), '.cl');
    DeleteFilesInFolder(ExpandConstant('{app}\Scripts'), '.txt');
    // üîÅ Add/adjust folders/extensions here as needed
  end;
end;
